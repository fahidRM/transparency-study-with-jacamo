/*
   JaCaMo Gradle build file

*/

plugins {
  id "ca.coglinc.javacc" version "2.4.0"
  // see https://github.com/johnmartel/javaccPlugin
}

defaultTasks 'jar'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version '1.0'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    //maven { url "http://jacamo.sourceforge.net/maven2/" }
    maven { url "https://raw.github.com/jacamo-lang/mvn-repo/master" }
    maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
    //maven { url "https://jade.tilab.com/maven/" }

    //flatDir {       dirs 'lib'    }
}


dependencies {
    compile group: 'org.jason'      , name: 'jason'   , version: '2.6.2' , changing: true, transitive: false
    compile group: 'org.jacamo'     , name: 'cartago' , version: '3.1-SNAPSHOT' , changing: true, transitive: false
    compile group: 'org.jacamo'     , name: 'jaca'    , version: '3.1-SNAPSHOT' , changing: true, transitive: false

    compile group: 'org.jacamo'     , name: 'moise'   , version: '1.0', changing: true, transitive: false
    compile group: 'org.jacamo'     , name: 'npl'     , version: '0.5'          , changing: true, transitive: false


    compile group: 'guru.nidi'      , name: 'graphviz-java',      version: '0.7.0'
    compile group: 'org.gradle'     , name: 'gradle-tooling-api', version: '6.8.3'

    runtime group: 'org.jacamo'     , name: 'intmas'  , version: '1.0.0'

    runtime group: 'org.jacamo'     , name: 'sai'     , version: '0.4', changing: true , transitive: false
    runtime "org.antlr:antlr4:4.8"

    runtime group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.30'

    //runtime group: 'com.tilab.jade' , name: 'jade'    , version: '4.5.0'
    runtime group: 'net.sf.ingenias', name: 'jade'    , version: '4.3'
    runtime group: 'org.apache.ant' , name: 'ant'     , version: '1.10.5'

    testCompile 'junit:junit:4.12'

    // used in cartago web infrastructure
    compile "io.vertx:vertx-core:3.7.0"
    compile "io.vertx:vertx-sync:3.7.0"
    compile "io.vertx:vertx-unit:3.7.0"
    compile "io.vertx:vertx-web:3.7.0"
    compile "io.vertx:vertx-web-client:3.7.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.8"
}

// copy dep jars and scritps into build directory
task copyJarsToBuild {
    inputs.files configurations.runtime.files
    inputs.dir   'scripts'
    outputs.dir  'build/libs'
    outputs.dir  'build/scripts'
    doLast {
        copy {
            from configurations.runtime
            into 'build/libs'
            //exclude '**/jason-2.0.jar'
        }
        copy {
            from 'scripts'
            into 'build/scripts'
            include '**'
            filter {
                String line -> line.replaceAll("JacamoREL=[0-9]+\\.[0-9]+", "JacamoREL="+project.version)
            }
            filter {
                String line -> line.replaceAll("JasonREL", file(project.configurations.compile.find { it.name.startsWith("jason-") }).name)
            }
        }
    }
}

compileJavacc {
    inputDirectory = file('src/main/javacc')
    outputDirectory = file(project.buildDir.absolutePath + '/generated/javacc/jacamo/project/parser')
}


jjdoc {
    outputDirectory = file(project.projectDir.absolutePath + '/doc')
    //arguments = [text: 'true']
}


jar {
    baseName 'jacamo'
    manifest {
        attributes 'Main-Class': 'jacamo.util.ConfigGUI',
                   'Specification-Title': 'JaCaMo framework',
                   'Specification-Version': project.version,
                   'Implementation-Version': new Date().toString(),
                   'Class-Path': file(project.configurations.compile.find { it.name.startsWith("jason-") }).name
    }

    from('src') {
        include '/test/jacamo/**/*.asl'
        include '/test/jacamo/**/*.jcm'
    }

    //from ( configurations.compile.collect { zipTree(it) } ) {
    //    include 'jason/**/*Factory.class', 'jason/**/Config.class'
    //}

    /*doFirst {
        Properties props = new Properties()
        props.put('version', '')
        props.put('release', project.version)
        props.store(new File(project.buildDir.absolutePath +'/classes/main/jacamo.dist.properties').newWriter(), null)
    }*/
}
jar.dependsOn(copyJarsToBuild)

clean {
    delete 'bin'
    delete 'readme.html'
    delete 'release-notes.html'
    delete fileTree('doc') { include '*.html'  }
    delete fileTree('doc/tutorials/hello-world')  { include '**/*.html'  }
    delete fileTree('doc/tutorials/coordination') { include '*.html'  }
    delete fileTree('doc/tutorials/gold-miners')  { include '**/*.html'  }
    delete fileTree('src')  { include '**/bin/**'  }
    delete fileTree('src')  { include '**/*.log'  }

    delete fileTree('examples')  { include '**/build/**'  }
    delete fileTree('examples')  { include '**/bin/**'  }
    delete fileTree('demos')     { include '**/build/**'  }
    delete fileTree('demos')     { include '**/bin/**'  }

    def emptyDirs = []
    fileTree (dir: 'examples').visit {
        def File f = it.file
        if (f.isDirectory() ) {
            def children = project.fileTree(f).filter { it.isFile() }.files
            if (children.size() == 0) {
                emptyDirs << f
            }
        }
    }
    fileTree (dir: 'demos').visit {
        def File f = it.file
        if (f.isDirectory() ) {
            def children = project.fileTree(f).filter { it.isFile() }.files
            if (children.size() == 0) {
                emptyDirs << f
            }
        }
    }
    emptyDirs.reverseEach { it.delete() }
}

task config (dependsOn: 'jar') {
    /*main = 'jacamo.util.Config'
    classpath sourceSets.main.runtimeClasspath
    workingDir project.buildDir.absolutePath + '/libs'

    doFirst {
        delete System.getProperty("user.home")+'/.jacamo'
    }*/

    doLast {
        println '------'
        println ''
        println 'you can set the JaCaMo home directory with the following command:'
        println '     export JACAMO_HOME='+project.buildDir.absolutePath
        println ''
        println 'and also change your PATH with:'
        println '     export PATH=$JACAMO_HOME/scripts:$PATH'
        println ''
        println '-----'
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'jacamo'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar (type : Jar) {
    baseName 'jacamo'
    classifier = 'sources'
    from sourceSets.main.allSource
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            artifactId 'jacamo'
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        file.whenMerged { cp ->
            cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/javacc', null) )
        }

        file.whenMerged { cp ->
            logger.lifecycle "Removing duplicate classpath entries from eclipse for project '${project.name}'"

            Map entryByPath = cp.entries.groupBy { entry -> entry.path }
            entryByPath.each { key, values ->
              if (values.size() > 1) {
                def entry = values.first()
                if (entry.kind == 'src') {
                  entry.includes = []
                  entry.excludes = []
                }
                int index = cp.entries.indexOf entry
                logger.lifecycle "Removing ${values.collect { it.path }}"
                cp.entries.removeAll values
                logger.lifecycle "Adding ${entry.path}"
                cp.entries.add index, entry
              }
            }
       }
    }
}

// transfer local maven artifact to our maven2 repository at sourceforge
/*task publishMavenSF(dependsOn: [ 'build', 'publishToMavenLocal']) {
    doLast {
        def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/jacamo'
        def tdir = System.getenv('USERSF')+',jacamo@web.sf.net:/home/project-web/jacamo/htdocs/maven2/org/jacamo/jacamo'
        exec {
            commandLine 'scp', '-r', project.version, tdir
            workingDir wdir
        }
        exec {
            commandLine 'cp', 'maven-metadata-local.xml', 'maven-metadata.xml'
            workingDir wdir
        }
        exec {
            commandLine 'scp', 'maven-metadata.xml', tdir
            workingDir wdir
        }
    }
}*/

task publishMavenGitHub(dependsOn: [ 'build', 'publishToMavenLocal']) {
  doLast {
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/jacamo'
    def rdir = '../jacamo-mvn-repo'
    exec {
        commandLine 'git', 'pull'
        workingDir rdir
    }
    copy {
        from wdir
        include project.version+'/**'
        into rdir + '/org/jacamo/jacamo'
    }
    exec {
        commandLine 'git', 'add', '*'
        workingDir rdir
    }
    exec {
        commandLine 'git', 'commit', '-a', '-m', 'new version of jacamo '+project.version
        workingDir rdir
    }
    exec {
        commandLine 'git', 'push'
        workingDir rdir
    }
  }
}

javadoc {
    options.showAll()
    options.encoding('UTF-8')
    options.setUse(true)
    options.author(true)
    options.version(true)
    options.windowTitle('JaCaMo API')
    options.docTitle('JaCaMo API')
    options.footer('<a href=http://jacamo.sf.net>JaCaMo Site</a>')
    destinationDir = file("${buildDir}/../doc/api")
    doFirst {
        javadoc.title = 'JaCaMo API'
        javadoc.options.docTitle = javadoc.title
    }
}


// Builds the distribution, documentation and a new release
task doc(dependsOn: [ 'jjdoc', 'javadoc', 'renderAsciiDocs']) {
}

task renderAsciiDocs(type: Exec) {
    commandLine 'find', '.', '-name', '*.adoc', '-exec', 'scripts/asciidoc-docker', '{}', ';'
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml', srcdir: 'src')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm', srcdir: 'examples')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm', srcdir: 'demos')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm,**/*.txt', srcdir: 'doc')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '*.gradle', srcdir: '.')
}


// Builds the distribution, documentation and a new release
task release(dependsOn: [ 'build', 'doc', 'fixTab', 'zipRel']) {
}

// copy files used to build a release
task copyRel {
    doLast {
        copy {
            from '.'
            into 'build/jacamo-'+project.version
            include '*.html'
            include 'LICENSE'
            include 'doc/**'
        }
        copy {
            from 'build/scripts'
            into 'build/jacamo-'+project.version+'/scripts'
        }
        copy {
            from libsDir
            from configurations.runtime
            into 'build/jacamo-'+project.version+'/'+libsDirName
            include '**/*.jar'
            //exclude '**/jason-2.0.jar'
        }
        copy {
            from docsDir
            into 'build/jacamo-'+project.version+'/doc'
        }
        copy {
            from 'examples'
            into 'build/jacamo-'+project.version+'/examples'
        }
        copy {
            from 'demos'
            into 'build/jacamo-'+project.version+'/demos'
        }
        copy {
            from 'src/main/resources/templates'
            into 'build/jacamo-'+project.version+'/src/templates'
        }
        delete fileTree('build/jacamo-'+project.version+'/doc')  { include '**/*.adoc', '**/Makefile', '**/publish.sh'  }
    }
}

// creates a zip file for a release
task zipRel(type: Zip, dependsOn: copyRel) {
    from 'build/jacamo-'+project.version
    baseName 'jacamo'
}

task runauction (type: JavaExec, dependsOn: 'classes') {
    workingDir 'examples/auction'
    main = 'jacamo.infra.JaCaMoLauncher'
    args 'auction.jcm'
    classpath sourceSets.main.runtimeClasspath
}
